plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.ossLicenses)
    alias(libs.plugins.googleServices)
    alias(libs.plugins.firebaseCrashlytics)
    alias(libs.plugins.hiltAndroid)
}

android {
    compileSdkVersion 36
    namespace 'com.d4rk.androidtutorials.java'
    defaultConfig {
        applicationId 'com.d4rk.androidtutorials.java'
        minSdk 23
        targetSdk 36
        versionCode 51
        versionName '5.0.2'
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        resourceConfigurations += ['en', 'de', 'es', 'fr', 'hi', 'hu', 'in', 'it', 'ja', 'ro', 'ru', 'tr', 'sv', 'bg', 'pl', 'uk']
    }

    buildTypes {
        release {
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            multiDexEnabled true
            debuggable true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    buildFeatures {
        viewBinding true
        dataBinding true
        buildConfig true
    }

    bundle {
        storeArchive {
            enable true
        }
    }
}

dependencies {

    // Google Play services and related UI components
    implementation libs.bundles.google.core

    // Firebase
    implementation platform(libs.firebase.bom)
    implementation libs.bundles.firebase.services

    // Jetpack libraries
    implementation libs.bundles.androidx.core
    implementation libs.bundles.androidx.lifecycle

    // Shared UI tooling and visuals
    implementation libs.bundles.ui.toolkit

    // Networking and serialization
    implementation libs.bundles.networking

    // Dependency injection
    implementation libs.hilt.android

    // Annotation processors
    annotationProcessor libs.hilt.compiler
    annotationProcessor libs.androidx.room.compiler
    annotationProcessor libs.glide.compiler

    // Testing
    testImplementation libs.bundles.unitTest
}